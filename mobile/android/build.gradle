// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.24'

        // Code Quality
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.4"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Apply plugins to all subprojects
subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        buildUponDefaultConfig = true
        allRules = false
        config.setFrom("$rootDir/config/detekt/detekt.yml")
        baseline = file("$rootDir/config/detekt/baseline.xml")
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.4"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// ktlint - Kotlin code style
configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest:ktlint:0.50.0") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlintCheck(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "**/*.kt", "**/*.kts", "!**/build/**"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "**/*.kt", "**/*.kts", "!**/build/**"
}

// Add ktlint check to regular check task
tasks.named("check") {
    dependsOn("ktlintCheck")
}
